#version 450

#extension GL_ARB_separate_shader_objects : enable
#extension GL_ARB_shading_language_420pack : enable

layout (binding = 0, rgba32f) readonly uniform image2DArray gbuffer;
layout (binding = 1, r32f) readonly uniform image2D occlusion;
layout (binding = 2, rgba32f) readonly uniform image2D diffuse_image;
layout (binding = 3, rgba32f) readonly uniform image2D specular_image;
layout (binding = 4, rgba32f) writeonly uniform image2D dest_image;
layout (binding = 5, rgba32f) writeonly uniform image2D bloom_image;
layout (binding = 7) uniform sampler3D light_extinction;
layout (binding = 9) uniform sampler3D view_extinction;
layout (binding = 10, rgba32f) readonly uniform image2D scattering;

#include "push_constants.h"
#include "constants.h"
#include "volumetric.h"

layout (std430, binding = 6) buffer Output {
  uint value;
  float scale;
} tone;

layout(local_size_x = 16, local_size_y = 16 ) in;

void main()  {
  const ivec2 screen_pos = ivec2( gl_GlobalInvocationID.xy );
  const vec3 pos = imageLoad( gbuffer, ivec3( screen_pos, 0 ) ).xyz;
  vec4 pos_in_screen = dynamic_uniforms.projection_matrix * dynamic_uniforms.camera_matrix * vec4( pos, 1.0 );
  pos_in_screen /= pos_in_screen.w;
  const vec3 pos_in_froxel = pos_in_screen.xyz * 0.5 + 0.5;
  const vec4 albedo = imageLoad( gbuffer, ivec3( screen_pos, 1 ) );
  
  const vec3 ambient = dynamic_uniforms.ambient * dynamic_uniforms.light_energy * imageLoad( occlusion, screen_pos ).r * albedo.rgb;
  const vec4 diffuse = imageLoad( diffuse_image, screen_pos );
  const vec3 specular = imageLoad( specular_image, screen_pos ).rgb;
  const vec3 incoming_rate = texture( light_extinction, pos_in_froxel ).rgb;
  const vec3 outgoing_rate = texture( view_extinction, pos_in_froxel ).rgb;
  const vec3 s = imageLoad( scattering, screen_pos ).rgb;

  const vec3 total = ( ( ambient + ( diffuse.rgb + specular ) * incoming_rate ) * outgoing_rate + s ) / tone.scale;
  const vec3 bloom = max( total - vec3( 1.0, 1.0, 1.0 ), vec3( 0.0, 0.0, 0.0 ) );

  imageStore( dest_image, screen_pos, vec4( total, albedo.a ) );
  imageStore( bloom_image, screen_pos, vec4( bloom, 1.0 ) );
}

