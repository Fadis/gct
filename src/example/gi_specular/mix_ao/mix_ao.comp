#version 450

#extension GL_ARB_separate_shader_objects : enable
#extension GL_ARB_shading_language_420pack : enable

layout (binding = 0, rgba32f) readonly uniform image2DArray gbuffer;
layout (binding = 1, rgba32f) readonly uniform image2D occlusion;
layout (binding = 2, rgba32f) readonly uniform image2D ssr_image;
layout (binding = 3, rgba32f) readonly uniform image2D diffuse_image;
layout (binding = 4, rgba32f) readonly uniform image2D specular_image;
layout (binding = 5, rgba32f) writeonly uniform image2D dest_image;
layout (binding = 6, rgba32f) writeonly uniform image2D bloom_image;

#include "push_constants.h"

layout (std430, binding = 7) buffer Output {
  uint value;
  float scale;
} tone;

layout(local_size_x = 16, local_size_y = 16 ) in;

void main()  {
  const ivec2 screen_pos = ivec2( gl_GlobalInvocationID.xy );
  const vec4 albedo = imageLoad( gbuffer, ivec3( screen_pos, 1 ) );
  
  const vec4 ambient_diffuse_occlusion = imageLoad( occlusion, screen_pos );
  const vec3 ambient = dynamic_uniforms.ambient * dynamic_uniforms.light_energy * ambient_diffuse_occlusion.a * albedo.rgb;
  const vec3 ambient_diffuse = ambient_diffuse_occlusion.rgb * albedo.rgb;
  const vec3 ambient_specular = imageLoad( ssr_image, screen_pos ).rgb;
  const vec4 diffuse = imageLoad( diffuse_image, screen_pos );
  const vec3 specular = imageLoad( specular_image, screen_pos ).rgb;
  const vec3 total = ( ambient_diffuse + ambient_specular + ambient + diffuse.rgb + specular ) / tone.scale;
  const vec3 bloom = max( total - vec3( 1.0, 1.0, 1.0 ), vec3( 0.0, 0.0, 0.0 ) );

  imageStore( dest_image, screen_pos, vec4( total, albedo.a ) );
  imageStore( bloom_image, screen_pos, vec4( bloom, 1.0 ) );
}

