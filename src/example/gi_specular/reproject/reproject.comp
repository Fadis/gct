#version 450

#extension GL_ARB_separate_shader_objects : enable
#extension GL_ARB_shading_language_420pack : enable

layout (binding = 0, rgba32f) readonly uniform image2DArray gbuffer;
layout (binding = 1, rgba32f) readonly uniform image2D src_diffuse;
layout (binding = 2, rgba32f) readonly uniform image2D src_specular;
layout (binding = 3, rgba32f) writeonly uniform image2D dest_diffuse;
layout (binding = 4, rgba32f) writeonly uniform image2D dest_specular;
layout (binding = 5, r32ui) uniform uimage2D depth_image;
layout (binding = 6, r32ui) readonly uniform uimage2D src_history;
layout (binding = 7, r32ui) writeonly uniform uimage2D dest_history;

layout(local_size_x = 16, local_size_y = 16 ) in;

#include "push_constants.h"

void main()  {
  const ivec2 screen_pos = ivec2( gl_GlobalInvocationID.xy );
  vec3 normal = imageLoad( gbuffer, ivec3( screen_pos, 7 ) ).xyz;
  const vec3 pos = imageLoad( gbuffer, ivec3( screen_pos, 0 ) ).xyz;
  const bool visible = dot( normal, dynamic_uniforms.eye_pos.xyz - pos ) > 0.0;
  if( !visible ) {
    return;
  }
  const ivec3 image_size = imageSize( gbuffer );
  const ivec2 image_right_bottom = image_size.xy /*- ivec2( 1, 1 )*/;
  vec4 new_screen_pos = dynamic_uniforms.projection_matrix * dynamic_uniforms.camera_matrix * vec4( pos, 1.0 );
  new_screen_pos /= new_screen_pos.w;
  if( new_screen_pos.x < -1.0 || new_screen_pos.x > 1.0 ) {
    return;
  }
  if( new_screen_pos.y < -1.0 || new_screen_pos.y > 1.0 ) {
    return;
  }
  if( new_screen_pos.z < -1.0 || new_screen_pos.z > 1.0 ) {
    return;
  }
  new_screen_pos.xyz *= 0.5;
  new_screen_pos.xyz += 0.5;
  const ivec3 new_screen_pos_in_integer = ivec3( new_screen_pos.xyz * ivec3( image_right_bottom, 65535 ) );

  uint existing_depth = imageLoad( depth_image, new_screen_pos_in_integer.xy ).r;
  for( uint retry = 0; retry != 4; retry++ ) {
    if( existing_depth > uint( new_screen_pos_in_integer.z ) ) {
      uint result = imageAtomicCompSwap( depth_image, new_screen_pos_in_integer.xy, existing_depth, uint( new_screen_pos_in_integer.z ) );
      if( result == existing_depth ) {
        vec4 diffuse = imageLoad( src_diffuse, screen_pos );
        vec4 specular = imageLoad( src_specular, screen_pos );
        if( isnan( diffuse.r ) ) return;
        if( isnan( diffuse.g ) ) return;
        if( isnan( diffuse.b ) ) return;
        if( isnan( diffuse.a ) ) return;
        if( isinf( diffuse.r ) ) return;
        if( isinf( diffuse.g ) ) return;
        if( isinf( diffuse.b ) ) return;
        if( isinf( diffuse.a ) ) return;
        if( isnan( specular.r ) ) return;
        if( isnan( specular.g ) ) return;
        if( isnan( specular.b ) ) return;
        if( isnan( specular.a ) ) return;
        if( isinf( specular.r ) ) return;
        if( isinf( specular.g ) ) return;
        if( isinf( specular.b ) ) return;
        if( isinf( specular.a ) ) return;
        imageStore( dest_diffuse, new_screen_pos_in_integer.xy, diffuse );
        imageStore( dest_specular, new_screen_pos_in_integer.xy, specular );
        uint history = imageLoad( src_history, screen_pos ).r;
        imageStore( dest_history, new_screen_pos_in_integer.xy, uvec4( history, 0, 0, 0 ) );
        return;
      }
      existing_depth = result;
    }
    else {
      return;
    }
  }
}

