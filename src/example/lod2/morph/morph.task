#version 460

#extension GL_ARB_separate_shader_objects : enable
#extension GL_ARB_shading_language_420pack : enable
#extension GL_EXT_nonuniform_qualifier : enable

// メッシュシェーダー拡張を使う
#extension GL_EXT_mesh_shader : enable

#include <gct/scene_graph.h>
#include <gct/global_uniforms.h>

// 1つのローカルワークグループには32スレッドが含まれる
layout(local_size_x = 32, local_size_y = 1 ) in;

// タスクペイロード
struct task_data {
  // ノードにぶら下がるメッシュの情報を指すID
  uint instance_id;
  // メッシュの情報を指すID
  uint primitive_id;
  // このローカルワークグループが担当するメッシュレットのオフセット
  uint task_offset;
};
taskPayloadSharedEXT task_data td;

void main() {
  // メッシュの情報を得る
  const resource_pair_type id = resource_pair[ push_constants.instance + gl_GlobalInvocationID.y ];
  const instance_resource_index_type inst = instance_resource_index[ id.inst ];
  const primitive_resource_index_type prim = primitive_resource_index[ inst.prim ];
  const mesh_type mesh = mesh_pool[ prim.mesh ];
  
  // メッシュの頂点数を96で割ってメッシュレットの数を求める
  const uint invocation_count = mesh.vertex_count / ( 32 * 3 ) + ( ( mesh.vertex_count % ( 32 * 3 ) == 0 ) ? 0u : 1u );
  // このローカルワークグループから実行する必要があるメッシュシェーダーのローカルワークグループの数を求める
  const uint local_invocation_count = min( invocation_count - id.offset * 32u, 32u );
  // タスクペイロードにどのメッシュのどの部分を描くべきかを書く
  td.instance_id = id.inst;
  td.primitive_id = inst.prim;
  td.task_offset = id.offset;
  // メッシュシェーダーを実行する
  EmitMeshTasksEXT( local_invocation_count, 1, 1 );
}

