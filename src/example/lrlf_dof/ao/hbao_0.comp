#version 450

#extension GL_ARB_separate_shader_objects : enable
#extension GL_ARB_shading_language_420pack : enable

#include "push_constants.h"

layout (binding = 0, rgba32f) readonly uniform image2D position;
layout (binding = 1, rg32f) writeonly uniform image2DArray temp_image;

layout(binding = 4,std430) buffer MatrixPool {
  mat4 matrix_pool[];
};

layout(local_size_x = 512, local_size_y = 1 ) in;

const int hbao_kernel_size = 64;
shared vec3[gl_WorkGroupSize.x+hbao_kernel_size*2+1] hbao_cache;
#include <gct/hbao.h>

void main() {
  const ivec2 image_size = imageSize( temp_image ).xy;
  const ivec2 screen_pos = ivec2( gl_GlobalInvocationID.xy );
  const uint lcpos = gl_LocalInvocationID.x;
  {
    const uint hcpos = lcpos + gl_WorkGroupSize.x;
    ivec2 lsample_pos = screen_pos;
    lsample_pos.x = lsample_pos.x - hbao_kernel_size;
    lsample_pos.y = lsample_pos.y;
    ivec2 hsample_pos = lsample_pos;
    hsample_pos.x += int( gl_WorkGroupSize.x );
    lsample_pos.x = max( min( lsample_pos.x, image_size.x ), 0 );
    lsample_pos.y = max( min( lsample_pos.y, image_size.y ), 0 );
    hsample_pos.x = max( min( hsample_pos.x, image_size.x ), 0 );
    hsample_pos.y = max( min( hsample_pos.y, image_size.y ), 0 );
    hbao_cache[ lcpos ] = ( matrix_pool[ global_uniforms.camera_matrix ] * vec4( imageLoad( position, lsample_pos ).xyz, 1.0 ) ).xyz;
    if( hcpos < gl_WorkGroupSize.x+hbao_kernel_size*2+1 ) {
      hbao_cache[ hcpos ] = ( matrix_pool[ global_uniforms.camera_matrix ] * vec4( imageLoad( position, hsample_pos ).xyz, 1.0 ) ).xyz;
    }
    barrier();
  }
  if( screen_pos.x >= image_size.x ) return;
  if( screen_pos.y >= image_size.y ) return;
  const float sum = hbao( lcpos + hbao_kernel_size );
  imageStore( temp_image, ivec3( screen_pos, 0 ), vec4( sum, 0.0, 0.0, 0.0 ) );
}

