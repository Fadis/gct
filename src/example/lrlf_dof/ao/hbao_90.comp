#version 450

#extension GL_ARB_separate_shader_objects : enable
#extension GL_ARB_shading_language_420pack : enable
#extension GL_EXT_nonuniform_qualifier : enable

layout(local_size_x = 1, local_size_y = 512 ) in;

#include <gct/global_uniforms.h>
#include <gct/scene_graph/matrix_pool.h>
#include <gct/scene_graph/image_pool.h>

const int hbao_kernel_size = 64;
shared vec3[gl_WorkGroupSize.y+hbao_kernel_size*2+1] hbao_cache;
#include <gct/hbao.h>

layout(push_constant) uniform PushConstants {
  uint src;
  uint dest;
} push_constants;

void main() {
  const ivec2 image_size = imageSize( image_pool32f[ nonuniformEXT( push_constants.dest ) ] ).xy;
  const ivec2 screen_pos = ivec2( gl_GlobalInvocationID.xy );
  const uint lcpos = gl_LocalInvocationID.y;
  {
    const uint hcpos = lcpos + gl_WorkGroupSize.y;
    ivec2 lsample_pos = screen_pos;
    lsample_pos.y = lsample_pos.y - hbao_kernel_size;
    ivec2 hsample_pos = lsample_pos;
    hsample_pos.y += int( gl_WorkGroupSize.y );
    lsample_pos.x = max( min( lsample_pos.x, image_size.x - 1 ), 0 );
    lsample_pos.y = max( min( lsample_pos.y, image_size.y - 1 ), 0 );
    hsample_pos.x = max( min( hsample_pos.x, image_size.x - 1 ), 0 );
    hsample_pos.y = max( min( hsample_pos.y, image_size.y - 1 ), 0 );
    hbao_cache[ lcpos ] = ( matrix_pool[ global_uniforms.camera_matrix ] * vec4( imageLoad( image_pool32f[ nonuniformEXT( push_constants.src ) ], lsample_pos ).xyz, 1.0 ) ).xyz;
    if( hcpos < gl_WorkGroupSize.y+hbao_kernel_size*2+1 ) {
      hbao_cache[ hcpos ] = ( matrix_pool[ global_uniforms.camera_matrix ] * vec4( imageLoad( image_pool32f[ nonuniformEXT( push_constants.src ) ], hsample_pos ).xyz, 1.0 ) ).xyz;
    }
    barrier();
  }
  if( screen_pos.x >= image_size.x ) return;
  if( screen_pos.y >= image_size.y ) return;
  const float sum = hbao( lcpos + hbao_kernel_size );
  imageStore( image_pool32f[ nonuniformEXT( push_constants.dest ) ], screen_pos, vec4( sum, 0.0, 0.0, 0.0 ) );
}

