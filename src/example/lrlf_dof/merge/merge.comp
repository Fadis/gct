#version 450

#extension GL_ARB_separate_shader_objects : enable
#extension GL_ARB_shading_language_420pack : enable

layout (binding = 1, rgba32f) readonly uniform image2DArray src_image;
layout (binding = 2, rgba32f) writeonly uniform image2D dest_image;
layout (binding = 3, rgba32f) writeonly uniform image2D bloom_image;
layout (binding = 9) uniform sampler2D skyview;

#include "push_constants.h"
#include <gct/constants.h>
#include <gct/sky.h>

layout (std430, binding = 6) buffer Output {
  uint value;
  float scale;
} tone;

layout(binding = 11,std430) buffer MatrixPool {
  mat4 matrix_pool[];
};

layout(local_size_x = 16, local_size_y = 16 ) in;

void main()  {
  const ivec2 screen_pos = ivec2( gl_GlobalInvocationID.xy );
  vec4 pos = matrix_pool[ global_uniforms.screen_to_world_matrix ] * vec4( vec2( screen_pos )/vec2( gl_NumWorkGroups * gl_WorkGroupSize ) * 2.0 - 1.0, 0.0, 1.0 );
  pos /= pos.w;
  vec4 pos_in_screen = matrix_pool[ global_uniforms.projection_matrix ] * matrix_pool[ global_uniforms.camera_matrix ] * pos;
  pos_in_screen /= pos_in_screen.w;
  const vec3 pos_in_froxel = pos_in_screen.xyz * 0.5 + 0.5;
  
  const vec3 eye_dir = normalize( pos.xyz - global_uniforms.eye_pos.xyz );
  const vec2 latitude_longitude = world_to_llar( eye_dir );
  const vec2 skyview_texcoord = vec2(
    ( latitude_longitude.y + pi ) / ( 2.0 * pi ),
    1.0 - skyview_latitude_to_texcoord( latitude_longitude.x )
  );
  vec3 total = bt709_to_xyz * texture( skyview, skyview_texcoord ).rgb;
  //vec3 total = vec3( 0.0, 0.0, 0.0 );

  vec4 far = max(
    imageLoad( src_image, ivec3( screen_pos, 1 ) ) +
    imageLoad( src_image, ivec3( screen_pos, 3 ) ) +
    imageLoad( src_image, ivec3( screen_pos, 5 ) ) +
    imageLoad( src_image, ivec3( screen_pos, 7 ) ) +
    imageLoad( src_image, ivec3( screen_pos, 9 ) ),
    vec4( 0.0, 0.0, 0.0, 0.0 )
  );
  total = mix( total, far.rgb, min( far.a * 3.0, 1.0 ) );
  //total = mix( total, vec3( 1.0 - min( far.a * 1.5, 1.0 ), far.a <= 0.1 ? 1.0 : 0.0, 0.0 ), min( far.a * 1.5, 1.0 ) );
  vec4 near = max(
    imageLoad( src_image, ivec3( screen_pos, 0 ) ) +
    imageLoad( src_image, ivec3( screen_pos, 2 ) ) +
    imageLoad( src_image, ivec3( screen_pos, 4 ) ) +
    imageLoad( src_image, ivec3( screen_pos, 6 ) ) +
    imageLoad( src_image, ivec3( screen_pos, 8 ) ),
    vec4( 0.0, 0.0, 0.0, 0.0 )
  );
  total = mix( total, near.rgb, min( near.a * 1.0, 1.0 ) );
  
  total /= tone.scale;
  const vec3 bloom = max( total - vec3( 1.0, 1.0, 1.0 ), vec3( 0.0, 0.0, 0.0 ) );

  imageStore( dest_image, screen_pos, vec4( total, 1.0 ) );
  imageStore( bloom_image, screen_pos, vec4( bloom, 1.0 ) );
}

