#version 460

#extension GL_ARB_separate_shader_objects : enable
#extension GL_ARB_shading_language_420pack : enable
#extension GL_EXT_nonuniform_qualifier : enable
#extension GL_EXT_mesh_shader : enable
#extension GL_EXT_shader_16bit_storage : enable
#extension GL_EXT_shader_8bit_storage : enable

#define GCT_ENABLE_8BIT_16BIT_STORAGE
#include <gct/scene_graph.h>
#include <gct/global_uniforms.h>
#include <gct/scene_graph/accessor.h>
#include <gct/global_uniforms.h>

layout(local_size_x = 32, local_size_y = 1 ) in;

layout(triangles, max_vertices = 96, max_primitives = 32) out;

layout (location = 0) out vec4 output_position[];
layout (location = 1) out vec3 output_normal[];
layout (location = 2) out vec3 output_tangent[];
layout (location = 3) out vec2 output_tex_coord[];
layout (location = 4) out vec4 output_optflow[];
layout (location = 5) flat out vec4 output_id[];
layout (location = 6) out vec4 output_previous_position[];

struct task_data {
  uint resource_pair_id;
  mat4 l2w;
  mat4 l2s;
};
taskPayloadSharedEXT task_data td;

void main() {
  const resource_pair_type id = resource_pair[ push_constants.instance + td.resource_pair_id ];
  const primitive_resource_index_type prim = primitive_resource_index[ id.prim ];
  const mesh_type mesh = mesh_pool[ prim.mesh ];

  const mat4 l2w = td.l2w;
  const mat4 l2s = td.l2s;
  
  const uint max_vertex_count = gl_WorkGroupSize.x * 3u;

  const uint meshlet_id = gl_WorkGroupID.x;
  const uint vertex_count = min( mesh.vertex_count - meshlet_id * max_vertex_count, max_vertex_count );
  
  const uint output_primitive_id = gl_LocalInvocationID.x;
  const uint output_vertex_id = output_primitive_id * 3u;
  
  SetMeshOutputsEXT( vertex_count, vertex_count / 3 );

  if( vertex_count <= output_vertex_id ) return; 
  

  const vertex_attribute v0 = read_vertex_attribute( mesh, gl_GlobalInvocationID.x * 3u + 0u );
  const vertex_attribute v1 = read_vertex_attribute( mesh, gl_GlobalInvocationID.x * 3u + 1u );
  const vertex_attribute v2 = read_vertex_attribute( mesh, gl_GlobalInvocationID.x * 3u + 2u );

  gl_MeshVerticesEXT[ output_vertex_id ].gl_Position = l2s * v0.position;
  gl_MeshVerticesEXT[ output_vertex_id + 1u ].gl_Position = l2s * v1.position;
  gl_MeshVerticesEXT[ output_vertex_id + 2u ].gl_Position = l2s * v2.position;
  output_position[ output_vertex_id ] = l2w * v0.position;
  output_position[ output_vertex_id + 1u ] = l2w * v1.position;
  output_position[ output_vertex_id + 2u ] = l2w * v2.position;
  output_normal[ output_vertex_id ] = ( l2w * v0.normal ).xyz;
  output_normal[ output_vertex_id + 1u ] = ( l2w * v1.normal ).xyz;
  output_normal[ output_vertex_id + 2u ] = ( l2w * v2.normal ).xyz;
  output_tangent[ output_vertex_id ] = ( l2w * v0.tangent ).xyz;
  output_tangent[ output_vertex_id + 1u ] = ( l2w * v1.tangent ).xyz;
  output_tangent[ output_vertex_id + 2u ] = ( l2w * v2.tangent ).xyz;
  output_tex_coord[ output_vertex_id ] = v0.tex_coord0.xy;
  output_tex_coord[ output_vertex_id + 1u ] = v1.tex_coord0.xy;
  output_tex_coord[ output_vertex_id + 2u ] = v2.tex_coord0.xy;
  output_optflow[ output_vertex_id ] = vec4( 0, 0, 0, 0 );
  output_optflow[ output_vertex_id + 1u ] = vec4( 0, 0, 0, 0 );
  output_optflow[ output_vertex_id + 2u ] = vec4( 0, 0, 0, 0 );
  output_id[ output_vertex_id ] = vec4( id.inst, id.prim, 0, 0 );
  output_id[ output_vertex_id + 1u ] = vec4( id.inst, id.prim, 0, 0 );
  output_id[ output_vertex_id + 2u ] = vec4( id.inst, id.prim, 0, 0 );
  gl_PrimitiveTriangleIndicesEXT[ output_primitive_id ] = uvec3( output_vertex_id, output_vertex_id + 1, output_vertex_id + 2 );
}

