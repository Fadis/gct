#version 460

#extension GL_ARB_separate_shader_objects : enable
#extension GL_ARB_shading_language_420pack : enable
#extension GL_EXT_nonuniform_qualifier : enable
#extension GL_EXT_mesh_shader : enable
#extension GL_KHR_shader_subgroup_arithmetic : enable
#extension GL_KHR_shader_subgroup_ballot : enable

#include <gct/scene_graph.h>
#include <gct/global_uniforms.h>

layout(local_size_x = 32, local_size_y = 1 ) in;

struct task_data {
  uint resource_pair_id;
  mat4 l2w;
  mat4 l2s;
};
taskPayloadSharedEXT task_data td;

void main() {
  const resource_pair_type id = resource_pair[ push_constants.instance + gl_GlobalInvocationID.y ];
  const uint meshlet_block_id = gl_LocalInvocationID.x;
  const instance_resource_index_type inst = instance_resource_index[ id.inst ];

  const mat4 l2w = matrix_pool[ inst.world_matrix ];

  const mat4 w2s =
    matrix_pool[ global_uniforms.projection_matrix ] *
    matrix_pool[ global_uniforms.camera_matrix ];
  const mat4 l2s =
    w2s *
    l2w;
  td.l2w = l2w;
  td.l2s = l2s;

  const uint visibility = last_visibility_pool[ inst.visibility ];
  if( visibility == 0u ) return;
  const primitive_resource_index_type prim = primitive_resource_index[ id.prim ];
  const mesh_type mesh = mesh_pool[ prim.mesh ];
  const uint invocation_count = mesh.vertex_count / ( 32 * 3 ) + ( ( mesh.vertex_count % ( 32 * 3 ) == 0 ) ? 0u : 1u );
  uint aligned_invocation_count = ( invocation_count / 32 + ( ( ( invocation_count % 32 ) == 0u ) ? 0u : 1u ) )*32;
  uint visible_invocation_count = 0u;
  for( uint i = meshlet_block_id; i < aligned_invocation_count; i += 32 ) {
    const bool has_meshlet = ( i < invocation_count );
    const meshlet_type meshlet = meshlet_pool[ mesh.meshlet + ( has_meshlet ? i : 0u ) ];
    const bool visible = ( has_meshlet && ( prim.cull == 0 || ( dot( normalize( ( l2w * vec4( meshlet.normal.xyz, 1.0 ) ).xyz ), normalize( global_uniforms.eye_pos.xyz - meshlet.center.xyz ) ) >= meshlet.normal.w ) ) );
    const uvec4 visible_bits = subgroupBallot( visible );
    const uint offset = subgroupBallotExclusiveBitCount( visible_bits );
    const uint total = subgroupBallotBitCount( visible_bits );
    if( visible ) {
      meshlet_index_pool[ inst.meshlet_index + visible_invocation_count + offset ] = i;
    }
    visible_invocation_count += total;
  }
  td.resource_pair_id = gl_GlobalInvocationID.y;
  EmitMeshTasksEXT( visible_invocation_count, 1, 1 );
}

