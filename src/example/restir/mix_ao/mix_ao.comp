#version 450

#extension GL_ARB_separate_shader_objects : enable
#extension GL_ARB_shading_language_420pack : enable

layout (binding = 0, rgba32f) readonly uniform image2DArray gbuffer;
layout (binding = 1, r32f) readonly uniform image2D occlusion;
layout (binding = 2, rgba32f) readonly uniform image2D diffuse_image;
layout (binding = 3, rgba32f) readonly uniform image2D specular_image;
layout (binding = 4, rgba32f) writeonly uniform image2D dest_image;
layout (binding = 5, rgba32f) writeonly uniform image2D bloom_image;
layout (binding = 9) uniform sampler2D skyview;
layout (binding = 10, rgba32f) readonly uniform image2D scattering;
layout (binding = 12, rgba32f) readonly uniform image2D direct;

#include "push_constants.h"
#include "constants.h"
#include "sky.h"

layout(binding = 11,std430) buffer MatrixPool {
  mat4 matrix_pool[];
};

layout (std430, binding = 6) buffer Output {
  uint value;
  float scale;
} tone;

layout(local_size_x = 16, local_size_y = 16 ) in;

void main()  {
  const ivec2 screen_pos = ivec2( gl_GlobalInvocationID.xy );
  vec3 pos = imageLoad( gbuffer, ivec3( screen_pos, 0 ) ).xyz;
  const vec3 normal = imageLoad( gbuffer, ivec3( screen_pos, 1 ) ).xyz;
  bool background =( dot( normal, normal ) == 0.0 );
  vec4 pos_ = matrix_pool[ dynamic_uniforms.screen_to_world_matrix ] * vec4( vec2( screen_pos )/vec2( gl_NumWorkGroups * gl_WorkGroupSize ) * 2.0 - 1.0, 0.0, 1.0 );
  pos_ /= pos_.w;
  pos = background ? pos_.xyz : pos;
  vec4 pos_in_screen = matrix_pool[ dynamic_uniforms.projection_matrix ] * matrix_pool[ dynamic_uniforms.camera_matrix ] * vec4( pos, 1.0 );
  pos_in_screen /= pos_in_screen.w;
  const vec3 pos_in_froxel = pos_in_screen.xyz * 0.5 + 0.5;
  const vec4 albedo = imageLoad( gbuffer, ivec3( screen_pos, 1 ) );
  
  const vec3 eye_dir = normalize( pos - dynamic_uniforms.eye_pos.xyz );
  const vec2 latitude_longitude = world_to_llar( eye_dir );
  const vec2 skyview_texcoord = vec2(
    ( latitude_longitude.y + pi ) / ( 2.0 * pi ),
    1.0 - skyview_latitude_to_texcoord( latitude_longitude.x )
  );
  const vec3 skyview_color = /*vec3( skyview_texcoord, 0.0 );*/ texture( skyview, skyview_texcoord ).rgb;
  bool has_surface = dot( normal, normal ) != 0.0;

  const float occ = imageLoad( occlusion, screen_pos ).r;
  const vec3 ambient = dynamic_uniforms.ambient * occ * albedo.rgb;
  const vec3 direct = imageLoad( direct, screen_pos ).rgb;
  const vec4 diffuse = imageLoad( diffuse_image, screen_pos );
  const vec3 specular = imageLoad( specular_image, screen_pos ).rgb;
  const vec4 scat = imageLoad( scattering, screen_pos ).rgba;
  const vec3 total = (
    has_surface ?
    ( ( ambient + ( direct + diffuse.rgb * occ + specular * occ ) ) * scat.w + scat.rgb ) :
    skyview_color
  ) / tone.scale;
  const vec3 bloom = max( total - vec3( 1.0, 1.0, 1.0 ), vec3( 0.0, 0.0, 0.0 ) );
  
  //const float flow = length( imageLoad( gbuffer, ivec3( screen_pos, 5 ) ).xyz );

  //imageStore( dest_image, screen_pos, vec4( /*total*/vec3( occ, occ, occ ), albedo.a ) );
  //imageStore( bloom_image, screen_pos, vec4( /*bloom*/vec3( 0.0, 0.0, 0.0 ), 1.0 ) );
  imageStore( dest_image, screen_pos, vec4( total, albedo.a ) );
  imageStore( bloom_image, screen_pos, vec4( bloom, 1.0 ) );
}

