#version 450

#extension GL_ARB_separate_shader_objects : enable
#extension GL_ARB_shading_language_420pack : enable
#extension GL_KHR_shader_subgroup_basic : enable
#extension GL_KHR_shader_subgroup_arithmetic : enable

layout (binding = 0, rgba32f) readonly uniform image2DArray gbuffer;
layout (binding = 1, rgba32f) readonly uniform image2D diffuse_image;
layout (binding = 2, rgba32f) uniform image2D dest_image;
layout (binding = 3) uniform sampler2D environment_map;

layout(local_size_x = 16, local_size_y = 16 ) in;

#include "constants.h"
#include "poisson_sphere.h"
#include "random.h"
#include "push_constants.h"

mat2 get_rotate_matrix2( float angle ) {
  float s = sin( angle );
  float c = cos( angle );
  return mat2( c, s, -s, c );
}

vec3 diffuse_ambient_light(
  vec3 dir,
  vec3 albedo,
  float roughness,
  float metallicness
) {
  vec3 environment_diffuse = textureLod( environment_map, vec2( dir.x, dir.y ) * 0.5 + 0.5, 0.0 ).rgb * mix( albedo, vec3( 0, 0, 0 ), metallicness );
  return environment_diffuse;
}

void main() {
  const float kernel_size = 0.5;
  const ivec2 screen_pos = ivec2( gl_GlobalInvocationID.xy );
  const vec4 center_ = imageLoad( gbuffer, ivec3( screen_pos, 0 ) );
  const vec3 center = center_.xyz;
  const vec3 normal = imageLoad( gbuffer, ivec3( screen_pos, 2 ) ).xyz;
  if( normal == vec3( 0, 0, 0 ) ) {
    imageStore( dest_image, screen_pos, vec4( 0, 0, 0, 0 ) );
    return;
  }
  const vec2 mr = imageLoad( gbuffer, ivec3( screen_pos, 4 ) ).rg;
  const float metallicness = mr.r;
  const float roughness = mr.g;
  
  const mat3 rot3 = mat3( get_rotate_matrix2( rand1( vec2( screen_pos.xy ) ) * 2 * pi ) );

  const ivec3 image_size = imageSize( gbuffer );

  vec3 env_color = vec3( 0.0, 0.0, 0.0 );
  float scale = 0.0;
  for( int i = 0; i < poisson_sphere_sample_count; i++ ) {
    vec3 offset = ( rot3 * poisson_sphere[ i ] ) * kernel_size;
    if( dot( normal, offset ) < 0.0 ) {
      offset = -offset;
    }
    vec3 world_pos = center + offset;
    vec4 screen_pos = dynamic_uniforms.projection_matrix * dynamic_uniforms.camera_matrix * vec4( world_pos, 1.0 );
    screen_pos /= screen_pos.w;
    ivec2 sample_pos = ivec2(
      ( screen_pos.x * 0.5 + 0.5 ) * ( image_size.x - 1 ), 
      ( screen_pos.y * 0.5 + 0.5 ) * ( image_size.y - 1 )
    );
    sample_pos.x = max( min( sample_pos.x, image_size.x - 1 ), 0 );
    sample_pos.y = max( min( sample_pos.y, image_size.y - 1 ), 0 );
    const vec3 dir = normalize( offset );
    const float r = max( dot( normal, dir ), 0.0 );
    const vec4 sample_world_pos = imageLoad( gbuffer, ivec3( sample_pos, 0 ) );
    const vec3 sample_normal = imageLoad( gbuffer, ivec3( sample_pos, 2 ) ).xyz;
    const vec3 sample_diffuse = imageLoad( diffuse_image, sample_pos ).rgb;
    float travel_distance = distance( sample_world_pos.xyz, center );
    const vec3 actual_dir = normalize( sample_world_pos.xyz - center );
    const float sample_depth = sample_world_pos.w;
    float visibility = ( 1.0 - step( sample_depth, screen_pos.z ) );
    env_color +=
      mix(
        vec3( visibility ),
        sample_diffuse * 3000 * 1/max( travel_distance * travel_distance, 1.0 ) * max( dot( normal, actual_dir ), 0 ) * max( dot( sample_normal, -actual_dir ), 0 ),
        diffuse_ambient_light( dir, vec3( 1.0, 1.0, 1.0 ), roughness, metallicness ) * r
      );
    scale += r;
  }
  imageStore( dest_image, screen_pos, vec4( env_color / scale, 1.0 ) );
}

