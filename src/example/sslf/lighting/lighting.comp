#version 450

#extension GL_ARB_separate_shader_objects : enable
#extension GL_ARB_shading_language_420pack : enable

layout (binding = 0, rgba32f) readonly uniform image2DArray gbuffer;
layout (binding = 1, rgba32f) writeonly uniform image2D diffuse_image;
layout (binding = 2, rgba32f) writeonly uniform image2D specular_image;

layout(local_size_x = 16, local_size_y = 16 ) in;

#include "constants.h"
#include "push_constants.h"
#include "lighting.h"


struct punctual_light_parameter {
  vec4 local_position;
  vec4 world_position;
  vec4 local_direction;
  vec4 world_direction;
  vec4 energy;
  uint type;
  float light_size;
  float theta;
  float phi;
  float falloff;
  uint matrix;
  uint reserved[ 2 ];
};

layout(binding = 6,std430) buffer LightPool {
  punctual_light_parameter light_pool[];
};

void main()  {
  const ivec2 screen_pos = ivec2( gl_GlobalInvocationID.xy );
  vec3 normal = imageLoad( gbuffer, ivec3( screen_pos, 2 ) ).xyz;
  const vec3 pos = imageLoad( gbuffer, ivec3( screen_pos, 0 ) ).xyz;
  const vec2 mr = imageLoad( gbuffer, ivec3( screen_pos, 4 ) ).xy;
  const float roughness = mr.g;
  const float metallicness = mr.r;
  const vec4 albedo = imageLoad( gbuffer, ivec3( screen_pos, 1 ) ).rgba;
  const vec3 emissive = imageLoad( gbuffer, ivec3( screen_pos, 3 ) ).rgb;
  const float shadow_level = imageLoad( gbuffer, ivec3( screen_pos, 6 ) ).r; 
 
  const vec3 N = normal;
  const vec3 V = normalize(global_uniforms.eye_pos.xyz-pos);
  const vec3 L = normalize(light_pool[global_uniforms.light ].world_position.xyz-pos);

  vec3 diffuse = diffuse_with_mask(
    L, V, N, albedo.rgb, roughness, metallicness, emissive,
    light_pool[global_uniforms.light].energy.xyz,
    shadow_level
  );
  vec3 specular = specular_with_mask(
    L, V, N, albedo.rgb, roughness, metallicness,
    light_pool[global_uniforms.light].energy.xyz,
    shadow_level
  );
  imageStore( diffuse_image, screen_pos, vec4( diffuse, albedo.a ) );
  imageStore( specular_image, screen_pos, vec4( specular, 1.0 ) );
}

