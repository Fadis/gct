#version 450

#extension GL_ARB_separate_shader_objects : enable
#extension GL_ARB_shading_language_420pack : enable
#extension GL_EXT_nonuniform_qualifier : enable
#extension GL_EXT_shader_image_load_formatted : enable

#define GCT_USE_IMAGE_POOL_WITHOUT_FORMAT

#include <gct/global_uniforms.h>
#include <gct/constants.h>
#include <gct/scene_graph/matrix_pool.h>
#include <gct/scene_graph/image_pool.h>
#include <gct/scene_graph/texture_pool.h>
#include <gct/scene_graph/k+buffer.h>

layout(push_constant) uniform PushConstants {
  uint src;
  uint bloom;
  uint dest;
  uint star;
  uint flare;
} push_constants;


layout (std430, binding = 7) buffer Tone {
  uint value;
  float scale;
} tone;

layout (std430, binding = 13) buffer AFState {
  uint history;
  uint max_history;
  float depth;
  float znear;
  float zfar;
  float focal_distance;
  float lens_size;
  float sensor_size;
  float apeture_rotation;
  float visible_range;
} af_state;

layout(local_size_x = 16, local_size_y = 16 ) in;

void main()  {
  const ivec2 screen_pos = ivec2( gl_GlobalInvocationID.xy );
  const ivec2 image_size = imageSize( image_pool_2d[ nonuniformEXT( push_constants.dest ) ] ).xy;

  //const uint layer_index = uint( imageLoad( image_pool_2d_array[ nonuniformEXT( global_uniforms.gbuffer ) ], ivec3( screen_pos, 32 ) ).x );
  //const float mark = layer_index.x == 0 ? 0.0 : read_kplus_buffer_optflow16( global_uniforms.gbuffer, screen_pos, layer_index - 1u ).w;

  vec3 total = imageLoad( image_pool_2d[ nonuniformEXT( push_constants.src ) ], screen_pos ).rgb;

  float vignetting_factor = 1.0;
  {
    const float aspect_ratio = float( image_size.x )/float( image_size.y );
    // イメージセンサーの縦方向の長さと焦点距離の比の2乗を求める
    const float factor = ( af_state.sensor_size/aspect_ratio )/af_state.focal_distance;
    const float f2 = factor * factor;
    const vec2 uv = vec2( screen_pos )/vec2( image_size );
    // イメージセンサーの中心からピクセルまでの距離の2乗を求める
    const vec2 d = abs( uv - vec2( 0.5f, 0.5f ) ) * vec2( 2.0f * aspect_ratio, 2.0f );
    const float r2 = dot( d, d );
    // f2 * r2でtan^2θが求まる
    // θはレンズの軸とこのピクセルに映る物体の向きを表すベクトルのなす角
    // cosθ = 1/sqrt( tan^2θ + 1 )
    const float cos_theta = 1.0f / sqrt( f2 * r2 + 1.0f);
    // コサイン4乗則
    // 1.4はビネッティングの効果の強さを指定する係数
    vignetting_factor = 1.0 - max( 1.0 - cos_theta * cos_theta * cos_theta * cos_theta, 0.0 ) * 1.4;
  }
  total *= vignetting_factor;
  total += texture( texture_pool[ nonuniformEXT( push_constants.star ) ], vec2( screen_pos )/vec2( image_size ) ).rgb;
  total += texture( texture_pool[ nonuniformEXT( push_constants.flare ) ], vec2( screen_pos )/vec2( image_size ) ).rgb;

  total /= tone.scale;
  total = max( total, vec3( 0.0, 0.0, 0.0 ) );
  const vec3 bloom = max( total - vec3( 1.0, 1.0, 1.0 ), vec3( 0.0, 0.0, 0.0 ) );
  
  //imageStore( image_pool_2d[ nonuniformEXT( push_constants.dest ) ], screen_pos, vec4( mark == 0.0 ? total : vec3( 1.0, 0.0, 0.0 ), 1.0 ) );
  imageStore( image_pool_2d[ nonuniformEXT( push_constants.dest ) ], screen_pos, vec4( total, 1.0 ) );
  imageStore( image_pool_2d[ nonuniformEXT( push_constants.bloom ) ], screen_pos, vec4( bloom, 1.0 ) );
}

