#version 460

#extension GL_ARB_separate_shader_objects : enable
#extension GL_ARB_shading_language_420pack : enable
#extension GL_EXT_scalar_block_layout : enable

#include <gct/translate.h>
#include <gct/scene_graph/matrix_pool.h>

layout(push_constant) uniform PushConstants {
  vec4 offset;
  float far;
  float near;
  uint projection;
  uint camera;
} push_constants;

layout(local_size_x = 6, local_size_y = 1, local_size_z = 1 ) in;

vec3 up_vector[6] =vec3[](
  vec3(  0, -1,  0 ),
  vec3(  0, -1,  0 ),
  vec3(  0,  0,  1 ),
  vec3(  0,  0, -1 ),
  vec3(  0, -1,  0 ),
  vec3(  0, -1,  0 )
);

vec3 center_offset_vector[6] = vec3[](
  vec3(  1,  0,  0 ),
  vec3( -1,  0,  0 ),
  vec3(  0,  1,  0 ),
  vec3(  0, -1,  0 ),
  vec3(  0,  0,  1 ),
  vec3(  0,  0, -1 )
);

void main() {
  const uint face_index = gl_GlobalInvocationID.x;
  if( face_index >= 6 ) return;
  const vec3 pos = push_constants.offset.xyz;
  mat4 camera_matrix =
    lookat(
      pos,
      pos + center_offset_vector[ face_index ],
      up_vector[ face_index ]
    );
  matrix_pool[ push_constants.camera + face_index ] = camera_matrix;
  if( face_index >= 1 ) return;
  matrix_pool[ push_constants.projection ] =
    perspective_simple(
      push_constants.near,
      push_constants.far
    );
}

