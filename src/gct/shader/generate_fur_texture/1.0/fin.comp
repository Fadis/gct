#version 460

#extension GL_ARB_separate_shader_objects : enable
#extension GL_ARB_shading_language_420pack : enable
#extension GL_EXT_nonuniform_qualifier : enable
#extension GL_EXT_shader_image_load_formatted : enable

#define GCT_USE_IMAGE_POOL_WITHOUT_FORMAT

layout(local_size_x = 16, local_size_y = 16 ) in;

#include <gct/constants.h>
#include <gct/global_uniforms.h>
#include <gct/scene_graph/image_pool.h>
#include <gct/random.h>

layout(push_constant) uniform PushConstants {
  uint src;
  uint dest;
  float shell_texture_clamp_max;
  float shell_texture_clamp_min;
} push_constants;

const int kernel_size = 32;
shared vec3[gl_WorkGroupSize.x+kernel_size*2+1] cache;

void main() {
  const ivec2 screen_pos = ivec2( gl_GlobalInvocationID.xy );
  const uint image_height = imageSize( image_pool_2d[ nonuniformEXT( push_constants.dest ) ] ).y;
  const uint image_depth = imageSize( image_pool_2d[ nonuniformEXT( push_constants.src ) ] ).y;
  float total = 0.0;
  for( uint z = 0u; z != image_depth; ++z ) {
    const float height = imageLoad(
      image_pool_2d[ nonuniformEXT( push_constants.src ) ],
      ivec2( screen_pos.x, z )
    ).r;
    const float shell_texture_value = min(
      max(
        height - push_constants.shell_texture_clamp_min,
        0.0
      )/
      ( push_constants.shell_texture_clamp_max - push_constants.shell_texture_clamp_min ),
      1.0
    );
    const float y0 = float( screen_pos.y )/float( image_height );
    const float y1 = float( screen_pos.y + 1 )/float( image_height );
    const float width0 = 1.0 - y0 / shell_texture_value;
    const float width1 = 1.0 - y1 / shell_texture_value;
    const float coverage =
      ( y1 <= shell_texture_value ) ?
      ( width0 + width1 ) / 2.0 :
      0.0;
    total = 1.0 - ( 1.0 - total ) * ( 1.0 - coverage );
  }
  imageStore( image_pool_2d[ nonuniformEXT( push_constants.dest ) ], screen_pos, vec4( total, 0.0, 0.0, 1.0 ) );
}

