#version 450

#extension GL_ARB_separate_shader_objects : enable
#extension GL_ARB_shading_language_420pack : enable
#extension GL_EXT_nonuniform_qualifier : enable
#extension GL_EXT_shader_image_load_formatted : enable

layout(local_size_x = 16, local_size_y = 16 ) in;

#define GCT_USE_IMAGE_POOL_WITHOUT_FORMAT
#include <gct/constants.h>
#include <gct/global_uniforms.h>
#include <gct/lighting.h>
#include <gct/scene_graph/image_pool.h>
#include <gct/scene_graph/texture_pool.h>
#include <gct/scene_graph/light_pool.h>
#include <gct/scene_graph/matrix_pool.h>
#include <gct/scene_graph/ppll.h>
#include <gct/scene_graph/omnishadow.h>

layout(push_constant) uniform PushConstants {
  uint dest;
  uint unproject;
  uint light;
  uint ppll_state_id;
  uint gbuffer_format;
  uint gbuffer;
  uint position;
  uint start;
  uint next;
} push_constants;

void main()  {
  const ivec2 screen_pos = ivec2( gl_GlobalInvocationID.xy );
  const ivec2 src_pos = ivec2( screen_pos );

  ppll_iter iter = ppll_direct(
    push_constants.ppll_state_id,
    ppll_image( push_constants.gbuffer, push_constants.position, push_constants.start, push_constants.next ),
    screen_pos,
    push_constants.gbuffer_format
  );

  if( ppll_is_end( iter ) ) {
    return;
  }

  gbuffer_value p;
  p.albedo = ppll_get_albedo( iter );
  if( p.albedo.a == 0.0 ) {
    ppll_set( iter, push_constants.dest, vec4( 0, 0, 0, 0 ) );
    return;
  }

  const ivec2 image_size = imageSize( image_pool_2d_array[ nonuniformEXT( push_constants.dest ) ] ).xy;
  p.pos = ppll_get_pos_depth( iter ).xyz;
  p.occlusion = ppll_get_eo( iter ).w;
  p.shadow_level.x = pcss_omni( light_pool[ push_constants.light ].shadow_map, p.pos.xyz, light_pool[ push_constants.light ].world_position.xyz, light_pool[ push_constants.light ].light_size );
  
  if( p.shadow_level.x <= 0.0 ) {
    ppll_set( iter, push_constants.dest, vec4( 0.0, 0.0, 0.0, p.albedo.a ) );
    return;
  }

  const vec2 material = ppll_get_mrid( iter ).xy;
  p.metallic = material.x;
  p.roughness = material.y;
  p.tangent = ppll_get_tangent( iter ).xyz;

  const vec3 V = normalize(global_uniforms.eye_pos.xyz-p.pos);
  const vec3 L = normalize(light_pool[ push_constants.light ].world_position.xyz-p.pos);

  const vec3 diffuse = diffuse_kajiya_kay(
    L, V, p.tangent, p.albedo.rgb, p.roughness, p.metallic,
    light_pool[push_constants.light].energy.xyz,
    p.shadow_level.x * p.occlusion
  );
  
  const vec3 specular = specular_kajiya_kay_blinn_phong(
    L, V, p.tangent, p.albedo.rgb, p.roughness, p.metallic,
    light_pool[push_constants.light].energy.xyz,
    p.shadow_level.x * p.occlusion
  );

  ppll_set( iter, push_constants.dest, vec4( diffuse + specular, p.albedo.a ) );
}

