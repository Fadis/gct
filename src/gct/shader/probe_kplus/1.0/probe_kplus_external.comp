#version 450

#extension GL_ARB_separate_shader_objects : enable
#extension GL_ARB_shading_language_420pack : enable
#extension GL_EXT_nonuniform_qualifier : enable
#extension GL_EXT_shader_image_load_formatted : enable

#define GCT_USE_IMAGE_POOL_WITHOUT_FORMAT

layout(local_size_x = 16, local_size_y = 16 ) in;

#include <gct/scene_graph/image_pool.h>
#include <gct/scene_graph/k+buffer.h>

layout(push_constant) uniform PushConstants {
  uint dest;
  uint gbuffer_format;
  uint gbuffer;
  uint depth;
  uint src;
} push_constants;

void main()  {
  const ivec2 screen_pos = ivec2( gl_GlobalInvocationID.xy );
  const vec2 scale =
    vec2(imageSize( image_pool_2d_array[ nonuniformEXT( push_constants.gbuffer ) ] ).xy) /
    vec2(imageSize( image_pool_2d_array[ nonuniformEXT( push_constants.dest ) ] ).xy);
  const ivec2 src_pos = ivec2( screen_pos * scale );

  kplus_iter iter = kplus_begin(
    kplus_image( push_constants.gbuffer, push_constants.depth ),
    src_pos,
    push_constants.gbuffer_format
  );
  kplus_iter nearest = kplus_nearest(
    iter
  );
  const bool has_layer = !kplus_is_end( nearest );
  const vec4 value =
    has_layer ?
    kplus_get(
      nearest,
      push_constants.src
    ) : vec4( 1, 0, 0, 1 );
  imageStore( image_pool_2d[ nonuniformEXT( push_constants.dest ) ], screen_pos, value );
}

